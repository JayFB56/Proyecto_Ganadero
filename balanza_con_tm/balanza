
#include "HX711.h"
#include <TM1637Display.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <RTClib.h>
#include <Keypad.h>
#include "SPIFFS.h"
#include <WiFi.h>
#include <HTTPClient.h>
#include <ArduinoJson.h>
#include <WebServer.h>
#include <Preferences.h>
#include <DNSServer.h>

#define DT 15
#define SCK 21
#define DIO 4
#define CLK 5

#define SDA_PIN 18
#define SCL_PIN 19

#define BUZZER 23
#define LED_OK 16
#define LED_ERR 17

#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64

#define ROWS 4
#define COLS 4

const char* ssid = "THELAPTOP";
const char* password = "12345678";
const char* firebase_host = "https://lechefacil-22cf0-default-rtdb.firebaseio.com/";
const char* firebase_secret = "YPA0OWn7ARdlyFZP42WEo40FdGiYDz060MgkTk1e";

const char* ap_ssid = "Balanza";
const char* ap_password = "12345678";

char keys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};

byte rowP[ROWS] = {13, 12, 14, 27};
byte colP[COLS] = {26, 25, 33, 32};

Keypad keypad = Keypad(makeKeymap(keys), rowP, colP, ROWS, COLS);
WebServer server(80);
DNSServer dnsServer;
Preferences preferences;

HX711 scale;
TM1637Display display(CLK, DIO);
Adafruit_SSD1306 oled(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);
RTC_DS3231 rtc;

float calibration_factor = 3291.459961;
String code = "";

float samples[10];
int idx = 0;
bool filled = false;
float grams = 0;

unsigned long lastOLED = 0;
unsigned long msgUntil = 0;
bool showMsg = false;
String msgText = "";
bool wifiConnected = false;

bool fechaMode = false;
int fechaPos = 0;
int fechaDigit[8];
DateTime fechaTemp;
unsigned long fechaMillis = 0;

bool ampmMode = false;
int ampmPos = 0;
int ampmDigit[4];
DateTime ampmTemp;

float rawReadings[200];
int rawIndex = 0;
int rawCount = 0;
float rawAverage = 0;
bool taraAutomatica = true;
unsigned long lastTaraTime = 0;
const unsigned long taraInterval = 60000;

void beep(int f, int d) {
  tone(BUZZER, f, d);
  delay(d);
  noTone(BUZZER);
  digitalWrite(BUZZER, LOW);
}

void handleRoot() {
  String html = "<!DOCTYPE html><html><head><meta name='viewport' content='width=device-width, initial-scale=1'><style>";
  html += "body{font-family:Arial;margin:20px;background:#f0f0f0;}";
  html += ".container{max-width:600px;margin:0 auto;background:white;padding:20px;border-radius:10px;box-shadow:0 2px 10px rgba(0,0,0,0.1);}";
  html += "h2{color:#333;border-bottom:2px solid #4CAF50;padding-bottom:10px;}";
  html += "button{background:#4CAF50;color:white;border:none;padding:10px 20px;margin:5px;border-radius:5px;cursor:pointer;font-size:16px;}";
  html += "button:hover{background:#45a049;}";
  html += "input{padding:8px;margin:5px;border:1px solid #ddd;border-radius:4px;width:200px;}";
  html += ".data-box{background:#f9f9f9;padding:10px;margin:10px 0;border-radius:5px;border-left:4px solid #4CAF50;}";
  html += "</style></head><body>";
  html += "<div class='container'>";
  html += "<h2>Configuración de Balanza</h2>";
  
  html += "<div class='data-box'>";
  html += "<h3>Valores Actuales</h3>";
  html += "<p><strong>Peso:</strong> " + String(grams, 2) + " g</p>";
  html += "<p><strong>Factor de Calibración:</strong> " + String(calibration_factor, 6) + "</p>";
  html += "<p><strong>Modo Tara:</strong> " + String(taraAutomatica ? "Automática" : "Manual") + "</p>";
  html += "</div>";
  
  html += "<div class='data-box'>";
  html += "<h3>Calibración</h3>";
  html += "<p>Coloque un peso de 1kg conocido en la balanza</p>";
  html += "<button onclick=\"calibrar()\">Calibrar con 1kg</button>";
  html += "</div>";
  
  html += "<div class='data-box'>";
  html += "<h3>Tara</h3>";
  html += "<button onclick=\"tare()\">Tara Manual</button>";
  html += "<button onclick=\"toggleTara()\">" + String(taraAutomatica ? "Cambiar a Manual" : "Cambiar a Automática") + "</button>";
  html += "</div>";
  
  html += "<div class='data-box'>";
  html += "<h3>Lecturas Crudas</h3>";
  html += "<p><strong>Promedio (200 lecturas):</strong> <span id='promedio'>" + String(rawAverage, 2) + "</span></p>";
  html += "<button onclick=\"actualizarPromedio()\">Actualizar Promedio</button>";
  html += "<div style='max-height:200px;overflow-y:scroll;border:1px solid #ddd;padding:10px;margin-top:10px;'>";
  html += "<strong>Últimas 200 lecturas:</strong><br>";
  for(int i = 0; i < rawCount; i++) {
    html += String(rawReadings[i], 2) + "<br>";
  }
  html += "</div></div>";
  
  html += "<div class='data-box'>";
  html += "<h3>Factor de Calibración Manual</h3>";
  html += "<input type='number' id='factor' step='any' value='" + String(calibration_factor, 6) + "'>";
  html += "<button onclick=\"setFactor()\">Establecer Factor</button>";
  html += "</div>";
  
  html += "<script>";
  html += "function calibrar(){";
  html += "fetch('/calibrar?kg=1').then(r=>r.text()).then(alert);}";
  html += "function tare(){";
  html += "fetch('/tare').then(r=>r.text()).then(alert);}";
  html += "function toggleTara(){";
  html += "fetch('/toggleTara').then(r=>r.text()).then(()=>location.reload());}";
  html += "function setFactor(){";
  html += "let f=document.getElementById('factor').value;";
  html += "fetch('/setFactor?f='+f).then(r=>r.text()).then(alert);}";
  html += "function actualizarPromedio(){";
  html += "fetch('/promedio').then(r=>r.text()).then(d=>document.getElementById('promedio').innerText=d);}";
  html += "setInterval(()=>{fetch('/peso').then(r=>r.text()).then(d=>{";
  html += "document.querySelector('p strong').innerText = d + ' g';});},1000);";
  html += "</script></div></body></html>";
  
  server.send(200, "text/html", html);
}

void handleCalibrar() {
  if(server.hasArg("kg")) {
    float kg = server.arg("kg").toFloat();
    float lectura = scale.get_units(10);
    if(lectura != 0) {
      calibration_factor = lectura / kg;
      scale.set_scale(calibration_factor);
      server.send(200, "text/plain", "Calibración exitosa. Nuevo factor: " + String(calibration_factor, 6));
    } else {
      server.send(500, "text/plain", "Error: Lectura cero");
    }
  }
}

void handleTare() {
  scale.tare(50);
  server.send(200, "text/plain", "Tara realizada");
}

void handleToggleTara() {
  taraAutomatica = !taraAutomatica;
  server.send(200, "text/plain", taraAutomatica ? "Automática" : "Manual");
}

void handleSetFactor() {
  if(server.hasArg("f")) {
    calibration_factor = server.arg("f").toFloat();
    scale.set_scale(calibration_factor);
    server.send(200, "text/plain", "Factor establecido");
  }
}

void handlePromedio() {
  server.send(200, "text/plain", String(rawAverage, 2));
}

void handlePeso() {
  server.send(200, "text/plain", String(grams, 2));
}

void setupWebServer() {
  server.on("/", handleRoot);
  server.on("/calibrar", handleCalibrar);
  server.on("/tare", handleTare);
  server.on("/toggleTara", handleToggleTara);
  server.on("/setFactor", handleSetFactor);
  server.on("/promedio", handlePromedio);
  server.on("/peso", handlePeso);
  server.begin();
}

void sendToFirebase(String codigo, float peso, String fecha, String hora, String turno) {
  HTTPClient http;
  
  StaticJsonDocument<256> doc;
  doc["codigo"] = codigo;
  doc["peso"] = peso;
  doc["fecha"] = fecha;
  doc["hora"] = hora;
  doc["turno"] = turno;
  doc["timestamp"] = millis();

  String jsonString;
  serializeJson(doc, jsonString);

  String url;
  if (wifiConnected) {
    url = String(firebase_host) + "/registros.json";
  } else {
    url = "http://192.168.4.1/registros";
  }
  
  http.begin(url);
  http.addHeader("Content-Type", "application/json");

  int httpCode = http.POST(jsonString);
  
  if (httpCode > 0) {
    if (httpCode == 200 || httpCode == 201) {
      digitalWrite(LED_OK, HIGH);
      beep(2000, 120);
      msgText = "ENVIADO";
    } else {
      digitalWrite(LED_ERR, HIGH);
      beep(400, 300);
      msgText = "ERROR: " + String(httpCode);
    }
  } else {
    digitalWrite(LED_ERR, HIGH);
    beep(400, 300);
    msgText = "ERROR CONEXION";
  }
  
  http.end();
  msgUntil = millis() + 2000;
  showMsg = true;
}

void saveRecord() {
  DateTime now = rtc.now();

  int hour = now.hour();
  bool pm = hour >= 12;
  int displayHour = hour % 12;
  if (displayHour == 0) displayHour = 12;

  char date[11], time[6];
  sprintf(date, "%02d/%02d/%04d", now.day(), now.month(), now.year());
  sprintf(time, "%02d:%02d", displayHour, now.minute());

  sendToFirebase(code, grams, String(date), String(time), pm ? "pm" : "am");

  code = "";
}

void errorPeso() {
  digitalWrite(LED_ERR, HIGH);
  beep(400, 300);
  msgText = "COLOQUE PESO \nEN LA BALANZA";
  msgUntil = millis() + 2000;
  showMsg = true;
}

void setup() {
  Serial.begin(115200);

  pinMode(BUZZER, OUTPUT);
  digitalWrite(BUZZER, LOW);

  pinMode(LED_OK, OUTPUT);
  pinMode(LED_ERR, OUTPUT);

  WiFi.mode(WIFI_AP_STA);
  WiFi.softAP(ap_ssid, ap_password);
  
  dnsServer.start(53, "*", WiFi.softAPIP());
  
  Serial.println("\n\n[SETUP] Iniciando WiFi...");
  WiFi.begin(ssid, password);
  
  int attempts = 0;
  while (WiFi.status() != WL_CONNECTED && attempts < 20) {
    delay(500);
    Serial.print(".");
    attempts++;
  }
  
  if (WiFi.status() == WL_CONNECTED) {
    Serial.println("\n[WIFI] Conectado!");
    Serial.println("[WIFI] IP: " + WiFi.localIP().toString());
    wifiConnected = true;
    setupWebServer();
  } else {
    Serial.println("\n[WIFI] Modo AP solo");
    wifiConnected = false;
  }

  scale.begin(DT, SCK);
  scale.set_scale(calibration_factor);
  scale.tare(50);

  display.setBrightness(7);

  Wire.begin(SDA_PIN, SCL_PIN);
  oled.begin(SSD1306_SWITCHCAPVCC, 0x3C);
  oled.clearDisplay();
  oled.display();

  rtc.begin();
}

void loop() {
  dnsServer.processNextRequest();
  
  if (WiFi.status() == WL_CONNECTED && !wifiConnected) {
    wifiConnected = true;
    setupWebServer();
    Serial.println("[WIFI] Reconectado!");
  } else if (WiFi.status() != WL_CONNECTED && wifiConnected) {
    wifiConnected = false;
  }

  if (wifiConnected) {
    server.handleClient();
  }

  if (scale.is_ready()) {
    float raw = scale.get_units(1);
    samples[idx++] = raw;
    rawReadings[rawIndex++] = raw;
    if (rawIndex >= 200) rawIndex = 0;
    if (rawCount < 200) rawCount++;
    
    float sumRaw = 0;
    for(int i = 0; i < rawCount; i++) sumRaw += rawReadings[i];
    rawAverage = sumRaw / rawCount;
    
    if (idx >= 10) {
      idx = 0;
      filled = true;
    }
    if (filled) {
      float sum = 0;
      for (int i = 0; i < 10; i++) sum += samples[i];
      grams = sum / 10.0;
      if (grams < 0) grams = 0;

      int shown = (int)grams;
      if (shown > 9999) shown = 9999;
      display.showNumberDec(shown, true);
    }
  }

  if (taraAutomatica && millis() - lastTaraTime > taraInterval) {
    scale.tare(50);
    lastTaraTime = millis();
  }

  char key = keypad.getKey();
  if (key) {
    if (fechaMode) {
      if (key >= '0' && key <= '9') {
        if (fechaPos < 8) {
          fechaDigit[fechaPos] = key - '0';
          fechaPos++;
          beep(1800, 80);
        }
      } else if (key == '*') {
        if (fechaPos > 0) {
          fechaPos--;
          beep(1500, 80);
        }
      } else if (key == 'A') {
        if (fechaPos == 8) {
          int dia = fechaDigit[0]*10 + fechaDigit[1];
          int mes = fechaDigit[2]*10 + fechaDigit[3];
          int anio = fechaDigit[4]*1000 + fechaDigit[5]*100 + fechaDigit[6]*10 + fechaDigit[7];
          rtc.adjust(DateTime(anio, mes, dia, rtc.now().hour(), rtc.now().minute(), 0));
          fechaMode = false;
          beep(2000, 120);
        }
      } else if (key == 'C') {
        fechaMode = false;
        beep(1500, 80);
      } else if (key == 'D') {
        rtc.adjust(DateTime(fechaTemp.year(), fechaTemp.month(), fechaTemp.day(), rtc.now().hour(), rtc.now().minute(), 0));
        fechaMode = false;
        beep(2000, 120);
      }
    } else if (ampmMode) {
      if (key >= '0' && key <= '9') {
        if (ampmPos < 4) {
          ampmDigit[ampmPos] = key - '0';
          ampmPos++;
          beep(1800, 80);
        }
      } else if (key == '*') {
        if (ampmPos > 0) {
          ampmPos--;
          beep(1500, 80);
        }
      } else if (key == 'A') {
        if (ampmPos == 4) {
          int hour = ampmDigit[0]*10 + ampmDigit[1];
          int minute = ampmDigit[2]*10 + ampmDigit[3];
          if (hour < 24 && minute < 60) {
            rtc.adjust(DateTime(rtc.now().year(), rtc.now().month(), rtc.now().day(), hour, minute, 0));
          }
          ampmMode = false;
          beep(2000, 120);
        }
      } else if (key == 'C') {
        ampmMode = false;
        beep(1500, 80);
      } else if (key == 'D') {
        rtc.adjust(DateTime(ampmTemp.year(), ampmTemp.month(), ampmTemp.day(), ampmTemp.hour(), ampmTemp.minute(), 0));
        ampmMode = false;
        beep(2000, 120);
      }
    } else {
      if (key == '*') {
        if (code.length() > 0) {
          code.remove(code.length() - 1);
          beep(1500, 80);
        }
      } else if (key >= '0' && key <= '9') {
        if (code.length() < 6) {
          code += key;
          beep(1800, 80);
        }
      } else if (key == 'A') {
        if (grams > 0) saveRecord();
        else errorPeso();
      } else if (key == 'B') {
        fechaMode = true;
        fechaPos = 0;
        memset(fechaDigit, 0, sizeof(fechaDigit));
        DateTime now = rtc.now();
        fechaTemp = DateTime(now.year(), now.month(), now.day(), now.hour(), now.minute(), now.second());
        fechaMillis = millis();
        beep(1800, 80);
      } else if (key == '#') {
        ampmMode = true;
        ampmPos = 0;
        memset(ampmDigit, 0, sizeof(ampmDigit));
        DateTime now = rtc.now();
        ampmTemp = DateTime(now.year(), now.month(), now.day(), now.hour(), now.minute(), now.second());
        beep(1800, 80);
      }
    }
  }

  if (showMsg && millis() > msgUntil) {
    showMsg = false;
    digitalWrite(LED_OK, LOW);
    digitalWrite(LED_ERR, LOW);
  }

  if (millis() - lastOLED >= 300) {
    lastOLED = millis();

    oled.clearDisplay();
    oled.setTextColor(SSD1306_WHITE);

    if (showMsg) {
      oled.setTextSize(1);
      oled.setCursor(10, 28);
      oled.println(msgText);
      oled.display();
      return;
    }

    DateTime now = rtc.now();
    
    if (fechaMode) {
      oled.setTextSize(2);
      oled.setCursor(10, 20);
      for (int i = 0; i < 8; i++) {
        if (i < fechaPos) oled.print(fechaDigit[i]);
        else oled.print("_");
        if (i == 1 || i == 3) oled.print("/");
      }
      oled.display();
    } else if (ampmMode) {
      oled.setTextSize(2);
      oled.setCursor(10, 20);
      for (int i = 0; i < 4; i++) {
        if (i < ampmPos) oled.print(ampmDigit[i]);
        else oled.print("_");
        if (i == 1) oled.print(":");
      }
      oled.display();
    } else {
      int hour = now.hour();
      bool pm = hour >= 12;
      int displayHour = hour % 12;
      if (displayHour == 0) displayHour = 12;

      char dateLine[20];
      sprintf(dateLine, "%02d/%02d/%04d", now.day(), now.month(), now.year());
      
      char timeLine[10];
      sprintf(timeLine, "%02d:%02d %s", displayHour, now.minute(), pm ? "pm" : "am");

      oled.setTextSize(1);
      oled.setCursor(0, 0);
      oled.println(dateLine);
      
      oled.setCursor(70, 0);
      oled.println(timeLine);

      oled.drawLine(0, 16, 127, 16, SSD1306_WHITE);

      oled.setCursor(0, 22);
      oled.println("Codigo:");

      oled.setTextSize(3);
      oled.setCursor(10, 34);
      oled.println(code);

      oled.display();
    }
  }
}
